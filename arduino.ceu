#include "arduino/arduino.ceu"

output bool PIN_13; //led
input  bool PIN_02; //shoot button
input  bool PIN_12; //left button
input  bool PIN_04; //right button

event void shoot;
event void left;
event void right;

native _shootSound;
native _moveSound;

native/pre do
	#define buzzerPort 11 
	
	void shootSound(){
		tone(buzzerPort, 2093);
		delay(40);
		tone(buzzerPort, 1865);
		delay(83);
		noTone(buzzerPort);
	}

	void moveSound(){
		tone(buzzerPort, 880);
		delay(40);
		noTone(buzzerPort);
	}
end

par/and do
	//blink on-board led
	loop do
	    emit PIN_13(HIGH);
	    await 1s;
	    emit PIN_13(LOW);
	    await 1s;
	end
with
	//play buzzer
	loop do
		await shoot;
		_shootSound();
		await 100ms;
	end
with
	//play buzzer left
	loop do
		await left;
		_moveSound();
		await 100ms;
	end
with
	//play buzzer right
	loop do
		await right;
		_moveSound();
		await 100ms;
	end		
with
	//shoot button
    loop do
        var bool v = await PIN_02 until v==HIGH;
        emit shoot;
    end
with
	//left button
    loop do
        var bool v = await PIN_12 until v==HIGH;
        emit left;
    end
with
	//right button
    loop do
        var bool v = await PIN_04 until v==HIGH;
        emit right;
    end
end
